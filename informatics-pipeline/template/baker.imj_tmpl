// ***************************************************************************
// Resource name:  baker.imj_tmpl
// Written by:     Rob Svirskas
// Revision level: 1.0
// Date released:  2008-10-30
// Description:    After processing, this ImageJ macro template template
//                 implements the image processing flow for the Baker Lab's
//                 LSM images. ImageJ is used to create stack projections,
//                 a Z stack translation, and a 180 degree single-axis
//                 rotation.
// Parameters:     NONE
// ***************************************************************************

// $grammar{lsm_file}
requires("1.38x");

var method="[Maximum Intensity]";
var rmethod="[Brightest Point]";
var base='$grammar{lsm_file} Channel : ';
var Detect= newArray('Ch2-T2','Ch3-T2','Ch4-T2');
var Red   = newArray('Ch2-T2','Ch3-T2','Ch4-T2');
var Green = newArray('Ch1-T1','Ch2-T1','Ch3-T1');
var Blue  = newArray('Ch2-T2','Ch3-T2','Ch4-T2');
var single = 0;

print("Sequence start for $grammar{lsm_file}");
saveSettings();
loadStack();
mergeImages();
if (nSlices > 1) {
  createTranslation();
  selectWindow("$grammar{short_file}");
  createRotation();
}
createProjections();
terminateMacro();

function loadStack() {
  print("Opening $grammar{lsm_dir}/$grammar{lsm_file} with LSM Reader");
  run("LSM Reader","open=$grammar{lsm_dir}/$grammar{lsm_file}");
  if (nImages() == 0) {
    print("Opening $grammar{lsm_dir}/$grammar{lsm_file} with "
          + "Bio-Formats Importer");
    run("Bio-Formats Importer","open=$grammar{lsm_dir}/$grammar{lsm_file} "
        + "view=[Standard ImageJ] stack=Default split_channels");
    // The base changes for files opened with the Bio-Formats Importer!
    base='$grammar{lsm_file} - ';
    if (nImages() == 0) {
      exit("Could not open with Bio-Formats Importer");
    }
  }
  print("Image opened");
}

function mergeImages() {
  print("Merging " + nImages() + " images");
  if ((nImages() == 2) || (nImages() == 3))  {
    Found=0;
    for (i=0; i<Detect.length; i++) {
      DETECT = base + Detect[i];
      RED = base + Red[i];
      GREEN = base + Green[i];
      BLUE = base + Blue[i];
      if (getTitle() == DETECT) {
        print("Detected " + Detect[i]);
        if (bitDepth() == 16) {
          convert12bit(RED);
          convert12bit(GREEN);
          if ((nImages() == 3) && (bitDepth() == 16)) {
            convert12bit(BLUE);
          }
        }
        run("RGB Merge...","red=[" + RED + "] " + "green=[" + GREEN
            + "] blue=[" + BLUE + "]");
        Found=1;
      }
    }
    if (Found == 0) {
      exit("Could not match title " + getTitle());
    }
  }
  else {
    if (nImages() != 1) {
      print("Title: "+getTitle());
      exit("Image must be 2- or 3-channel (found " + nImages() + ")");
    }
    GREEN = getTitle();
    if (bitDepth() == 16) {
      convert12bit(GREEN);
    }
    run("RGB Merge...","red=*None* " + "green=[" + GREEN
        + "] blue=*None*");
    single = 1;
  }
  rename("$grammar{short_file}");
  print("Image merge complete");
}

function convert12bit(image) {
// Uncomment the following lines to fix non-ramped
// images with lots of red pixels...
//  method="[Average Intensity]";
//  rmethod="[Mean Value]";
  print("Converting from 12-bit");
  selectWindow(image);
// Straight set
  setMinAndMax(0,4095);
  run("8-bit");
  print("Conversion complete");
}

function createProjections() {
  print("Generating projections");
  // Reference+pattern total projection
  print("  Creating total projection (reference+pattern)");
  zProject();
  saveAs("Jpeg","$grammar{projection_dir}/$grammar{short_file}" + "_00.jpg");
  close();
  print("  Total projection complete");

  // Reference+pattern substacks
  print("  Creating substack projections (reference+pattern)");
  subStack=15;
  orgNum=nSlices;
  slNum=nSlices;
  while (slNum>=subStack) {
    slNum=slNum-subStack;
  }
  addNum=subStack-slNum;
  if (slNum>0) {
    for (t=1; t<=addNum; t++)  {
      setSlice(nSlices);
      run("Add Slice");
      run("Subtract...", "slice value=255");
    }
  }
  run("Grouped ZProjector", "group="+subStack+" projection=[Max Intensity]");
  filepath = "$grammar{projection_dir}/$grammar{short_file}" + "_01.jpg";
  run("Image Sequence... ", "format=Jpeg name=[$grammar{short_file}"
      + "_] start=1 digits=2 save=[" + filepath + "]");
  close();
  print("  Substack projections complete");

  if (single == 1) {
    return;
  }

  // Single channel projections
  print("  Splitting RGB");
  run("RGB Split");
  selectWindow("$grammar{short_file} (red)");
  close();
  selectWindow("$grammar{short_file} (blue)");
  close();
  print("  Split complete");

  // Clear memory
  clearMemory();

  // Pattern total projection
  print("  Creating total projection (pattern only)");
  run("RGB Merge...","red=*None* green=[$grammar{short_file} (green)] "
      + "blue=*None*");
  zProject();
  saveAs("Jpeg", "$grammar{projection_dir}/$grammar{short_file}"
         + "_ch2_00.jpg");
  close();
  print("  Total projection complete");

  // Pattern substacks
  print("  Creating substack projections (pattern only)");
  run("Grouped ZProjector","group=" + subStack + " projection=[Max Intensity]");
  filepath = "$grammar{projection_dir}/$grammar{short_file}" + "_ch2_01.jpg";
  run("Image Sequence... ", "format=Jpeg name=[$grammar{short_file}"
      + "_ch2_] start=1 digits=2 save=[" + filepath + "]");
  close();
  print("  Substack projections complete");
  print("Projections generated");
}

function zProject() {
  if (nSlices == 1) {
    return;
  }
  run("RGB Zprojector","red=" + method + " green=" + method
      + " blue=" + method + " power=2");
  if (method == "[Average Intensity]") {
    run("Sharpen");
    run("Enhance Contrast", "saturated=0.5");
  }
}

function createTranslation() {
  print("Generating translation");
  saveAs("avi","$grammar{translation_dir}/$grammar{translation_transient}");
  clearMemory();
  print("Translation generated");
}

function createRotation() {
  print("Generating rotation");
  run("3D Project...","projection=" + rmethod + " axis=Y-Axis slice=1 "
      + "initial=-90 total=180 rotation=5 lower=1 upper=255 opacity=0 "
      + "surface=100 interior=1 interpolate");
  if (method == "[Average Intensity]") {
    run("Enhance Contrast", "saturated=0.5 normalize_all");
  }
  saveAs("avi","$grammar{rotation_dir}/$grammar{rotation_transient}");
  close();
  clearMemory();
  print("Rotation generated");
}

function clearMemory() {
  d=call("ij.IJ.maxMemory");
  e=call("ij.IJ.currentMemory");
  for (trials=0; trials<3; trials++) {
    call("java.lang.System.gc");
    wait(100);
  }
}

function terminateMacro() {
  while (nImages()>0) {
    selectImage(nImages());
    run("Close");
  }
  print("Sequence stop");
  run("Quit","");
  exit();
}
